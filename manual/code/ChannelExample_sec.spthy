theory ChannelExample_sec
begin


/* Alice&Bob specification (sent over a secure channel)

	I: 	fresh(n)
	I -> R: n 

*/

/* Channel rules */

rule ChanOut_S:
        [ Out_S($A,$B,x) ]
      --[ ChanOut_S($A,$B,x) ]->
        [ !Sec($A,$B,x) ]

rule ChanIn_S:
        [ !Sec($A,$B,x) ]
      --[ ChanIn_S($A,$B,x) ]->
        [ In_S($A,$B,x) ]

/* Protocol */

rule I_1: 
	[ Fr(~n) ]
 	--[ Send($I,~n), Secret_I(~n) ]-> 
	[ Out_S($I,$R,~n) ]
		
rule R_1:
	[ In_S($I,$R,~n)  ]
	--[ Secret_R(~n), Authentic($I,~n) ]->
	[ ]

/* Security Properties */

lemma nonce_secret_initiator: 
	"All n #i #j. Secret_I(n) @i & K(n) @j ==> F"

lemma nonce_secret_receiver: 
	"All n #i #j. Secret_R(n) @i & K(n) @j ==> F"

lemma message_authentication: 
	"All I n #j. Authentic(I,n) @j ==> Ex #i. Send(I,n) @i &i<j"

end
