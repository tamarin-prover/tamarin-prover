theory MPAuth_MA_NoTellExceptOK
begin


/* 
============
INTRODUCTION
============
* Protocol:	MP-Auth_MA
* Modeler: 	Lara Schmid
* Date: 	March 2020
* Source:	[1] "Advancing the Formal Foundations for Voting Protocols", Lara Schmid, PhD Thesis, ETH ZÃ¼rich.

=========
RESULTS: 
=========

// for -D=ruleBased with Rule4($H,'noTellEx','ok',$X), but where $X is not the human's device (ensured by the axiom "notClaimwrtDevice")	
- lemma functional (exists-trace): falsified - no trace found (32 steps)


*/


/* built-ins */
builtins: signing, asymmetric-encryption, hashing, symmetric-encryption
functions: f/1,  m/1  // f/1 used for function in protocol, m/1 used to give message a type.


/* only allow human agent rules (corresponding to infallible human) */
#ifdef infallible
axiom only_infallible_humans:  
  	"not (Ex #k. H('Human') @k)" 
#endif

/* only allow for untrained human rules (corresponding to fallible human) */
#ifdef ruleBased
axiom only_fallible_humansRB:  
  	"not (Ex H x #k. H_role(H,x) @k)" 
#endif
#ifdef untrained
axiom only_fallible_humansU:  
  	"not (Ex H x #k. H_role(H,x) @k)"
#endif


/* untrained human rules*/ 
// insecure channels
rule H_fresh:
        [ Fr(~x) ]
	--[ Fresh($H,$x.1,~x), !HK($H,$x.1,~x),
		H($H) ]->
	[ !HK($H,$x.1,~x)]

rule H_send: 
	[ !HK($H,$x.1,x.2) ]
	--[ Send($H,$x.1,x.2),
		H($H) ]->
	[ Out(<$x.1,x.2>)]

rule H_receive: 
	[ In( <$x.1,x.2>) ]
	--[ Receive($H,$x.1,x.2), !HK($H,$x.1, x.2),
		H($H) ]->
	[ !HK($H,$x.1, x.2) ]

// secure channel
rule H_send_S:
  	[ !HK($H,$x.1,x.2) ]
	--[ Send($H,$x.1,x.2),
		H($H), To($A)]->
	[ Out_S($H,$A,$x.1,x.2) ]

rule H_receive_S:
	[ In_S( $A,$H, $x.1, x.2 ) ]
	--[ Receive($H,$x.1,x.2), !HK($H,$x.1,x.2), 
		H($H), From($A)]->
	[ !HK($H,$x.1,x.2) ]

rule H_send_S2: 
	[ !HK($H,$x.1,x.2), !HK($H,$y.1,y.2) ]
	--[ Send($H,$x.1,x.2), 
  	    Send($H,$y.1,y.2), 
	    H($H), To($A)]->
	[ Out_S($H,$A,<$x.1,$y.1>,<x.2,y.2>) ]


rule H_send_S3:
	[ !HK($H,$x.1,x.2), !HK($H,$y.1,y.2), !HK($H,$z.1,z.2) ]
	--[ Send($H,$x.1,x.2), 
	    Send($H,$y.1,y.2),
	    Send($H,$z.1,z.2), 
	    H($H), To($A)]->
	[ Out_S($H,$A,<$x.1,$y.1,$z.1>,<x.2,y.2,z.2>) ]

rule H_send_S4:
	[ !HK($H,$x.1,x.2), !HK($H,$y.1,y.2), !HK($H,$z.1,z.2), !HK($H,$w.1,w.2) ]
	--[     Send($H,$x.1,x.2), 
		Send($H,$y.1,y.2),
		Send($H,$z.1,z.2), 
		Send($H,$w.1,w.2), 
		H($H), To($A)]->
	[ Out_S($H,$A,<$x.1,$y.1,$z.1,$w.1>,<x.2,y.2,z.2,w.2>) ]


rule H_receive_S2:
	[ In_S( $A,$H, <$x.1,$y.1>, <x.2,y.2> ) ]
	--[ Receive($H,$x.1,x.2), 
		Receive($H,$y.1,y.2), !HK($H,$x.1,x.2),!HK($H,$y.1,y.2),
		H($H), From($A)]->
	[ !HK($H,$x.1,x.2),!HK($H,$y.1,y.2) ]


rule H_receive_S4:
	[ In_S( $A,$H, <$x.1,$y.1,$z.1,$v.1>,<x.2,y.2,z.2,v.2> ) ]
	--[     Receive($H,$x.1,x.2), 
		Receive($H,$y.1,y.2), 
		Receive($H,$z.1,z.2), 
		Receive($H,$v.1,v.2), !HK($H,$x.1,x.2), !HK($H,$y.1,y.2), !HK($H,$z.1,z.2), !HK($H,$v.1,v.2),
		H($H), From($A)]->
	[ !HK($H,$x.1,x.2), !HK($H,$y.1,y.2), !HK($H,$z.1,z.2), !HK($H,$v.1,v.2) ]



/* Channel rules */
rule ChanOut_S:
	[Out_S($A,$B,xn,x)]
	--[ChanOut_S($A,$B,xn,x)]->
	[!Sec($A,$B,xn,x)]

rule ChanIn_S:
	[!Sec($A,$B,xn,x)]
	--[ChanIn_S($A,$B,xn,x)]->
	[In_S($A,$B,xn,x)]

rule ChanOut_C:
	[Out_C($A,$B,xn,x)]
	--[ChanOut_C($A,$B,xn,x)]->
	[!Conf($B,xn,x)]

rule ChanIn_C:
	[!Conf($B,xn,x), In($A)]
	--[ChanIn_C($A,$B,xn,x)]->
	[In_C($A,$B,xn,x)]

rule ChanIn_CAdv:
	[In(<$A,$B,xn,x>)]
	--[]->
	[In_C($A,$B,xn,x)]

rule ChanOut_A:
	[Out_A($A,$B,xn,x)]
	--[ChanOut_A($A,$B,xn,x)]->
	[!Auth($A,xn,x), Out(<$A,$B,xn,x>)]
rule ChanIn_A:
	[!Auth($A,xn,x), In($B)]
	--[ChanIn_A($A,$B,xn,x)]->
	[In_A($A,$B,xn,x)]


/* setup */
rule passworddistribution:
	[Fr(~pw)]
	--[PasswordHS($X,$Y,~pw)]->
	[!Password($X,$Y,~pw)]
rule publickeyinfrastructure:
	[Fr(~skX)]
	--[LontermKey($X,~skX)]->
	[!Ltk($X, ~skX),!Pk($X, pk(~skX)), Out(pk(~skX)) ]

rule SetupAfterEA: 
  	[ !Password($H,$S,~pw), !Ltk($S, ~skS), Fr(~m), Fr(~rS), Fr(~rD)] 
  	--[ Setup($H), Roles($H,$D,$P,$S), Device($H,$D)
		, InitK($H,'D',$D), InitK($H,'P',$P), InitK($H,'S',$S), InitK($H,'m',m(~m)), InitK($H,'idH',$idH), InitK($H,'idS',$idS), InitK($H,'ok','ok')
		, Rule4($H,'noTellEx','ok',$X) //(axiom "notClaimwrtDevice" at end enforces that this is with respect to a term that is not the human's device)		
	]-> 
  	[ AgSt_D11($D,'D_7',< $H, $idH, pk(~skS), $idS, $S, ~rS, ~rD >)
	, AgSt_S9($S,'S_9',< ~skS, $idS, $H, $idH, ~rS, ~rD >) 
	                  
	, !HK($H,'D',$D), !HK($H,'P',$P), !HK($H,'S',$S), !HK($H,'m',m(~m)), !HK($H,'idH',$idH), !HK($H,'idS',$idS), !HK($H,'ok','ok')	
	, AgSt_H11($H,'H_11',< $D, $P, $S, m(~m), $idH, $idS >)
  	]

/* agent rules */
rule D_15: 
	[ AgSt_D11($D,'D_7',< $H, $idH, pk(~skS), $idS, $S, ~rS, ~rD >), In( senc(<m(~m),~rS2>,f(~rS,~rD)) ) ]
 	--[ D($D) ]->
	[ AgSt_D16($D,'D_16',< $H, $idH, pk(~skS), $idS, $S, ~rS, ~rD, m(~m), ~rS2 >), Out_S($D,$H,'m',m(~m)) ]
rule D_17: 
	[ AgSt_D16($D,'D_16',< $H, $idH, pk(~skS), $idS, $S, ~rS, ~rD, m(~m), ~rS2 >), In_S($H,$D,'ok','ok') ]
 	--[ Dfin(), DfromHx($D,$H,'ok','ok')]->
	[  Out(senc( f(m(~m),~rS2), f(~rS,~rD)) ) ]
		
 rule S_13:
	[ AgSt_S9($S,'S_9',< ~skS, $idS, $H, $idH, ~rS, ~rD >), In(m(~m)), Fr(~rS2) ]
	--[ ]->
	[ AgSt_S14($S,'S_14',< ~skS, $idS, $H, $idH, ~rS, ~rD, m(~m), ~rS2 >), Out( senc(<m(~m),~rS2>,f(~rS,~rD)) ) ]
rule S_19:
	[ AgSt_S14($S,'S_14',< ~skS, $idS, $H, $idH, ~rS, ~rD, m(~m), ~rS2 >), In( senc( f(m(~m),~rS2), f(~rS,~rD)) )]
	--[ Commit($S,$H,m(~m)) ]->
	[ ]

/* human agent rules */
rule H_12:
	[ AgSt_H11($H,'H_11',< $D, $P, $S, m(~m), $idH, $idS >) ]
	--[H_role($H,'1'), Send($H,'m',m(~m)) ]->
	[ AgSt_H12($H,'H_12',< $D, $P, $S, m(~m), $idH, $idS >), Out(<'m',m(~m)>) ]
rule H_16:           
	[ AgSt_H12($H,'H_12',< $D, $P, $S, m(~m), $idH, $idS >), In_S($D,$H,'m',m(~m)) ]
	--[H_role($H,'5'), Receive($H,'m',m(~m)), Send($H,'ok','ok'), To($D) ]->
	[ Out_S($H,$D,'ok','ok') ]

#ifdef ruleBased
/* Rule-Based Human Rules */

axiom noTell:
	"All l m #s #i. Rule3('Human','noTell',l) @s & Send('Human',l,m) @i ==> F "

axiom noTellEx:
	"All P l m #i #s. Rule4('Human','noTellEx',l,P)@s & Send('Human',l,m) @i ==> To(P) @i"

axiom noGet:	
	"( All l x #s #i. Rule3('Human','noGet',l) @s & Receive('Human',l,x) @i ==> F)"

axiom noGetEx:	
	"( All l x P #s #i. Rule4('Human','noGetEx',l,P) @s & Receive('Human',l,x) @i ==> From(P) @i)"

axiom ICompare:
	" All l y #s #i. Rule3('Human','ICompare',l)@s &  Receive('Human',l,y) @i ==> Ex #j. InitK('Human',l,y) @j"


#endif

/* axioms assumptions */

/* Every Human starts in the initial state */
axiom setup_for_IK:
	"All H #i. H(H) @i ==> Ex #j. Setup(H) @j & j<i"

/* No secure channels between humans exist. Humans
   must communicate over incecure channels.         
   This assumption is necessary to remove unsolved chains
   in the protocol model. It is not a strong restriction
   because the protocols do not rely on human to human 
   for security. */
axiom not_Chan_S_human_to_human:
	"All A B mn m #i #j #k.
		ChanOut_S(A,B,mn,m) @i & H(A) @j & H(B) @k ==> F"

/* Assumption: Two distinct roles are not executed by the same agent */
axiom notSameRole:
	"All H1 H2 D1 D2 P1 P2 S1 S2 #i #j. Roles(H1,D1,P1,S1) @i & Roles(H2,D2,P2,S2) @j ==>
		  not H1 = D1
		& not H1 = D2
		& not H1 = P1
		& not H1 = P2
		& not H1 = S1
		& not H1 = S2
		& not D1 = P1
		& not D1 = P2
		& not D1 = S1
		& not D1 = S2
		& not P1 = S1
		& not P1 = S2
	"
/* Assumption: Two humans do not share a single device */
axiom Device:
	"All H1 H2 D #i #j. Device(H1,D) @i & Device (H2,D) @j ==> H1=H2 "




/* lemmas */

lemma functional: exists-trace
	"(All H1 H2 #i #j. Setup(H1) @i & Setup(H2) @j ==> #i = #j) 
	& (Ex #n S m.
		Commit(S,'Human',m) @n
		)"


axiom notClaimwrtDevice:
	"All X #s1. Rule4('Human','noTellEx','ok',X) @s1 ==> not (Ex #s2. Device('Human',X) @s2)"

end

















 
