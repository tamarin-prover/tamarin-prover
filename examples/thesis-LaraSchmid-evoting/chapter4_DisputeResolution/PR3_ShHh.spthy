theory PR3_ShHh
begin

/* 
============
INTRODUCTION
============
* Protocol:	Protocol PR_3
* Modeler: 	Lara Schmid
* Date: 	March 2020
* Source:	[1] "Advancing the Formal Foundations for Voting Protocols", Lara Schmid, PhD Thesis, ETH ZÃ¼rich.
* Disclaimer:	The pupose of this protocl is to establish a possibility result.
		We specify a simple protocol with one voter, as this is sufficeint for demonstrating possibility.

========
RESULTS:
========

- lemma functional: verified 
- lemma DRvoterT: verified 
- lemma DRauth: verified 
 	

*/

/*============
SPECIFICATION:
============== */

/*=====================
Functions and equations 
======================= */
builtins: signing, multiset

functions: sg/2, ver/2 //new signing function

equations:
// our equation for modeling the verification of signatures
ver(sg(m, sk), pk(sk)) = m 
// the function models that given a signature, one can also see the signed plaintext (only if one has the public key, but adversary has all public keys). With this, we do not explicitly have to always send the message in plaintext and signed, but only the signed one is enough.



/* ==========
Channel rules
============= */

rule ChanOut_A: [Out_A($A,x)]--[ChanOut_A($A,x)]->[!Auth($A,x), Out(<$A,x>)]
rule ChanIn_A:	[!Auth($A,x)]--[ChanIn_A($A,x)]->[In_A($A,x)]


//reliable insecure channel from H to P: all messages sent on a reliable channel, are received unchanged

rule ChanOut_IR_HP:  	[ Out_IR_HP($A,$B,m) ]--[ ChanOut_IR_HP($A,$B,m) ]->[ Out(<$A,$B,m>) ] 
rule ChanIn_IR_HP:  	[ In(<$A,$B,m>) ]--[ ChanIn_IR_HP($A,$B,m) ]->[ In_IR_HP($A,$B,m) ] 
restriction Chan_R_HP:
	"All A B m #i. Send_R_HP(A,B,m) @i ==> Ex #j. Receive_R_HP(A,B,m) @j "
 

//reliable insecure channel from P to S: all messages sent on a reliable channel, are received unchanged

rule ChanOut_IR_PS:  	[ Out_IR_PS($A,$B,m) ]--[ ChanOut_IR_PS($A,$B,m) ]->[ Out(<$A,$B,m>) ] 
rule ChanIn_IR_PS:  	[ In(<$A,$B,m>) ]--[ ChanIn_IR_PS($A,$B,m) ]->[ In_IR_PS($A,$B,m) ] 
restriction Chan_R_PS:
	"All A B m #i. Send_R_PS(A,B,m) @i ==> Ex #j. Receive_R_PS(A,B,m) @j "
 


/* ======================
Adversary's fake evidence
====================== */
// the adversary can claim any term that he knows to be evidence
rule fakeEvidence:
	[ In(<ballot,evidence>) ] --[ Evidence(ballot,evidence) ]-> [ ] 


/* =====
Protocol
======== */

rule publickeyinfrastructure:
  [ Fr(~skX) ]--[ LongtermKey($X,~skX) ]-> [ !Ltk($X,~skX), !Pk($X,pk(~skX)), Out(pk(~skX)) ]

rule Setup: 
  	[ !Ltk('P',~skP) ] 
  	--[ 	Setup(),
		Voter('H'),
		Server('S'), Honest('S'),
		BB_pkP('BB',<'pkPs', pk(~skP) >)
	]-> 
  	[ AgSt_H0('H',< 'BB','P',$v>)
	, AgSt_BB0('BB',<'S'>)
	, AgSt_P0('P',<'S',~skP>)
	, AgSt_S0('S',<'empty'>)
	, Out_A('BB',<'pkPs', pk(~skP) >)
			
  	]

rule H_1:
	[ AgSt_H0($H, <$BB, $P, $v>) ]
	--[	Vote($H,$v), 
		Ballot($H,<$v,$H>),
		Send_R_HP($H,$P,<$v,$H>)
	]->
	[ AgSt_H1($H, <$BB, $P, $v>), Out_IR_HP($H,$P,<$v,$H>)
	]

rule P:
	[ AgSt_P0($P,<$S,~skP>), In_IR_HP($H,$P,<$v,$H>)]  // P implicitly (by pattern matching) only accepts ballots of the right format
	--[ Receive_R_HP($H,$P,<$v,$H>),
	    Evidence(<$v,$H>,sg(<$v,$H>,~skP)),
	    Send_R_PS($P,$S,<$v,$H>)
	]->
	[  Out_IR_PS($P,$S,<$v,$H>) ]

rule S_1: 
	[ AgSt_S0($S, <'empty'>), 
	  In_IR_PS($P,$S,<$v,$H>)  // S implicitly (by pattern matching) only accepts ballots of the right format
	 ] 
	--[ Receive_R_PS($P,$S,<$v,$H>)
	]->
	[ Out_A($S, <'bs', <$v,$H> >),
	  Out_A($S, <'vs', $v >)
	]
rule BB: 
	[ AgSt_BB0($BB,<$S>)
	, In_A($S, <'bs', bs >)
	, In_A($S, <'vs', vs >)
	]
 	--[ 	BB_recorded($BB,<'bs', bs >),
		BB_tallied($BB,<'vs', vs >),
		End()
	]->  
	[ Out_A($BB, <'bs', bs >)
	, Out_A($BB, <'vs', vs >)
	]
	
rule H_5: 
	[ AgSt_H1($H, <$BB, $P, $v>),
	  In_A($BB, <'bs', bRecorded >)
	]
	--[	Verify($H,<$v,$H>, bRecorded ) //this corresponds to the signal verifyC(H, <v,H>) in the thesis
	]->
	[ ]



/* =========
RESTRICTIONS
============ */
// there is only one setup
restriction OneSetup: 
	"All #i #j. Setup() @i & Setup() @j ==> #i = #j "


/* ====
LEMMAS 
======= */
/* ==================
Functional properties
=================== */

lemma functional: exists-trace
 	" Ex  #i #j BB #k #l #p skP
		.
		Vote('H','v') @i
		& Ballot('H',<'v','H'>) @j
		& Evidence(<'v','H'>,sg(<'v','H'>,skP)) @p
		& BB_recorded(BB,<'bs', <'v','H'> >) @k
		& BB_tallied(BB,<'vs','v' >) @k
		& Verify('H',<'v','H'>, <'v','H'> ) @l
		"
/*===========================
Dispute Resolution Properties
===========================*/
// As BB is honest, BB_recorded is always executed before End(), i.e., k<j holds as required by the Definition of DRvoterT. We thus leave this away for optimization.
lemma DRvoterT: 
	"All H b #i #j.
		Ballot(H,b) @i & End() @j & i<j 
	==> ( Ex BB #k .					
		BB_recorded(BB,<'bs', b >) @k )	
	|   //Faulty
	    ( Ex BB b1 #k #l skP #m . 
		BB_recorded(BB,<'bs', b1 >) @k
		& not (b1=b)
		& Evidence(b,sg(b,skP)) @l
		& BB_pkP(BB,<'pkPs', pk(skP) >)@m
		)"

lemma DRauth: 
	"All  S #i.
		Honest(S) @i 
	==> // not Faulty
	not (Ex b BB b1 #k #l skP #m . 
		BB_recorded(BB,<'bs', b1 >) @k
		& not (b1=b)
		& Evidence(b,sg(b,skP)) @l
		& BB_pkP(BB,<'pkPs', pk(skP) >)@m
		)"	

end

