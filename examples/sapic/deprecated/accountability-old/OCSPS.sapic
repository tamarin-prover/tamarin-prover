/*
 * Protocol:   OCSP Stapling
 * Modeler:   Robert KÃ¼nnemann
 * Date:  
 * Source:  https://tools.ietf.org/html/rfc6960
 * Status:  
 * Note:  execute with --heuristic=p 
 * */

theory OCSPS
begin

/* builtins: signing */
functions: 
    pk/1,sk/1[private],
    /* aenc/2, adec/2, */
    sign/2, verify/3,
    tlssign/2, tlsverify/3, //we model the authentication provides by TLS handshake + record protocol as a signature -> huge simplification
    true/0,
    S/1,
    NormalAct/0, UnusualAct/0

equations: 
    verify(sign(m, sk(i)),m, pk(sk(i))) = true,
    tlsverify(tlssign (m,sk),m, pk(sk)) = true

let S = 
    let 
        cert = sign(pk(sk),sk('CA'))
        m1   = cert
        /* m1   = sign(cert,sk('S')) */
        m1t  = <cert,~t,'good'>
        m1f  = <cert,~t,'revoked'>
        m2t   = <m1t,sign(m1t,sk('OCSP'))>
        m2f   = <m1f,sign(m1f,sk('OCSP'))>
        /* m3  = <m2t,tlssign(<content,~t>,sk)> // ~t -> part of the handshake (OCSCP extension) thus bound to TLS connection, modelled as signature */
    in
    new sk; // nevermind certification, just the date
    insert <'ocspstatus',cert>,'good';
    event Secret(cert,sk);
    // out (cert); outout as message m1 anyway...
    out (pk(sk));
    (!(
    lock 'unhacked';
        out (m1); // S, OCSP and Judge have preshared signature keys
        new content; // website content TODO could make it adversarial generated
        in(m2);
        out(m2);
        /* in (m2t); */
        /* out (m3); */
        unlock 'unhacked'
    )
    || // only server can be corrupted
    ( lock 'unhacked'; //adversary hacks
      lock 'ocspstatus'; // can be more specific
      lock 'time';
      lookup 'time' as ~t in
          event Corrupted('S');
          insert <'ocspstatus',cert>,'revoked';
          event Learn(sk);
          out(sk);
        // force new time...
        new ~tn; out(~tn);
        event Time(~tn);
        insert 'time', ~tn;
          unlock 'time';
          unlock 'ocspstatus'
          // no unlock 'unhacked'... server stops operating..
    )
    )

let Clock =
    /* insert 'time', '0'; */
    !( 
        lock 'time';
        /* lookup 'time' as ~t in */
            /* event Outdated(~t); */
            /* insert 'time', S(~t); */
        new ~tn; out(~tn);
        event Time(~tn);
        insert 'time', ~tn;
        unlock 'time'
    )
        
let OCSP = // Trusted responder
    let cert = sign(pk(sk),sk('CA'))
        m1   = cert
        /* m1   = sign(cert,sk('S')) */
        m1t  = <cert,~t,'good'>
        m1f  = <cert,~t,'revoked'>
        m1u  = <cert,~t,'unknown'>
        m2t   = <m1t,sign(m1t,sk('OCSP'))>
        m2f   = <m1f,sign(m1f,sk('OCSP'))>
        m2u   = <m1u,sign(m1u,sk('OCSP'))>
    in
    !( 
    in(m1);
    lock 'time';
    lock 'ocspstatus';
    lookup 'time' as ~t in
    lookup <'ocspstatus',cert> as status in (
            if status = 'good' then  (
                out(m2t);
                unlock 'time';
                unlock 'ocspstatus' )
            else 
                (
                out(m2f);
                unlock 'time';
                unlock 'ocspstatus'
             ))
    /* else 0 //TODO out(m2u) (judge needs to also distinguish...) */
        else out(m2u)
    )

let Judge =
    let cert = sign(pk(sk),sk('CA'))
        m1t  = <cert,~L_t,'good'>
        m1f  = <cert,~L_t,'revoked'>
        m2t   = <m1t,sign(m1t,sk('OCSP'))>
        m2f   = <m1f,sign(m1f,sk('OCSP'))>
        m1g  = <cert,~L_t,status>
        m2g   = <m1g,sign(m1g,sk('OCSP'))>
        /* m4  = <m2g,pk(sk),tlssign(<content,~L_t>,sk)>  //TODO should get rid of the pattern matching... */
    in
    /* in(m4); */
    in(m2g);
    lock 'time';
    lookup 'time' as ~L_tc in
        if status = 'good' then
            event BlameEmpty();
            /* event Judged(~L_tc,cert) */
            event Judged(~L_t,cert)
        else
            (if status = 'revoked' then
                event BlameS();
                /* event Judged(~L_tc,cert) */
                event Judged(~L_t,cert)
            else
                0 //only judge if response actually contains information
                )


let Proto = S || OCSP || Judge || Clock

// general form for accountabilty processes
Proto
|| ( event Corrupted('OCSP');
      out('c',sk('OCSP'))
      /* (  !(in('r',<$x,sid,m>);0) ) */
      /* TODO database access... */
    )
/* || !( in('c',<'corrupt',$x>); */ 
/*       event Corrupted($x); */
/*       out('c',sk($x)) */
/*       /1* (  !(in('r',<$x,sid,m>);0) ) *1/ */
/*       /1* TODO database access... *1/ */
/*     ) */

lemma sanity_logA_not_misbehaving:
    exists-trace
    "(Ex #i t cert. Judged(t,cert)@i )
    & not (Ex #i a. Corrupted(a)@i)
    "

lemma time_and_secet_distinct[reuse]:
    /* "All cert sk #i #j t. Secret(cert,sk)@i & Time(t)@j ==> not(t = sk)" */
    "not (Ex cert sk #i #j. Secret(cert,sk)@i & Time(sk)@j)"

lemma time_and_secet_distinct_two[reuse]:
    /* "All cert sk #i #j t. Secret(cert,sk)@i & Time(t)@j ==> not(t = sk)" */
    "not (Ex cert sk #i #j. Secret(cert,sk)@i & Insert('time',sk)@j)"

// proof in 7 steps, but not sure if useful...
lemma helper[reuse]:
    "All cert sk #i #j. Secret(cert,sk)@i & KU(sk)@j ==>
        Ex #k. Learn(sk)@k & i < k & k< j "

verdictfunction v1:
    "Ex #i #j t cert. Judged(t,cert)@i & BlameS()@j " -> <'S'>,
    otherwise -> empty 

// first: acc_S, assume OCSP is trusted, should hold
// then: acc_SOCSP, assume OCSP is not trusted, show lack of accountability
// future: make the protocol accountable. (Ilkan?)

restriction no_corrupt_ocsp: //TODO deactivate for acc_SOCSP
    "not (Ex #i. Corrupted('OCSP')@i)"

restriction no_corrupt_ca:
    "not (Ex #i. Corrupted('CA')@i)"

/* restriction corruption_not_in_vulnerable_timeframe: */
/*     "not ( Ex cert sk t #i #j #k #l. */
/*     Judged(t,cert)@i & Secret(cert,sk)@j */ 
/*     & Corrupted('S')@k */
/*     & Time(t)@l */
/*     & l < k)" */

lemma acc_S:
    v1 accounts [coarse] 
    for "not ( Ex cert sk t #i #j #k #l.
    Judged(t,cert)@i & Secret(cert,sk)@j & Learn(sk)@k & Time(t)@l
    & k < l
    /* & k < i */
    )
     /* | (Ex #i. Corrupted('OCSP')@i) */ //not the same as having the restriction... weird.
           "
    for parties <'S'>

  /* sanity_logA_not_misbehaving (exists-trace): verified (14 steps) */
  /* acc_S_excl_0_1 (all-traces): verified (2 steps) */
  /* acc_S_exh (all-traces): verified (2 steps) */
  /* acc_S_suf_0_0 (exists-trace): verified (39 steps) */
  /* acc_S_compl_empty_1 (all-traces): verified (6068 steps) */
  /* acc_S_min_0_0_0 (all-traces): verified (82 steps) */
  /* acc_S_uniq_0 (all-traces): verified (1003 steps) */

/* TODO need to put into separate file where restriction is removed */
/* lemma acc_SOCSP: */
/*     v1 accounts [coarse] */ 
/*     for "not ( Ex cert sk t #i #j #k #l. */
/*     Judged(t,cert)@i & Secret(cert,sk)@j & K(sk)@k & Time(t)@l & k < l) */
/*      /1* | (Ex #i. Corrupted('OCSP')@i) *1/ */
/*            " */
/*     for parties <'S','OCSP'> */

  /* sanity_logA_not_misbehaving (exists-trace): verified (14 steps) */
  /* acc_SOCSP_excl_0_1 (all-traces): verified (2 steps) */
  /* acc_SOCSP_exh (all-traces): verified (2 steps) */
  /* acc_SOCSP_suf_0_0 (exists-trace): verified (40 steps) */
  /* acc_SOCSP_compl_empty_1 (all-traces): verified (6101 steps) */
  /* acc_SOCSP_min_0_0_0 (all-traces): verified (82 steps) */
  /* acc_SOCSP_uniq_0 (all-traces): falsified - found trace (12 steps) */

end
