/*
 * Protocol:    Causality - Desert traveller protocol 
 * Modeler:   Robert KÃ¼nnemann, Ilkan Esiyok
 * Date:  
 * Source:  
 * Status:  
 * Note:  execute with --heuristic=p 
 * */

theory Causality_DesertTraveller
begin

builtins: signing

/* options: progress */

//Poisoned
let A = 0

//Shot
let B = 0

let Bottle = (in(~p); insert 'Bottle',~p; in(~s); delete 'Bottle') 
          +  (in(~s); delete 'Bottle') 

//Victim
let C =  insert 'Bottle', 'water'; 
         // .. some time passes
         lookup 'Bottle' as x in (
            if x=~p then event Poisoned(); event Control('0','1'); event Verdict()
            else event Healthy(); event Control('0','2'); event Verdict()
         )
         else (event Dehydration(); event Control('0','3'); event Verdict())

    
    
new ~p; new ~s;
(A || B || C || Bottle ||
 !(in ('c',<'corrupt',$x>); event Corrupted($x); 
     !(
       (if $x='A' then out('c',~p))
     ||
       (if $x='B' then out('c',~s))
     )
   )
)

lemma sanity:
	"
        All #i. Verdict()@i 
      ==> 
      ((Ex #j. Poisoned()@j) | (Ex #j. Dehydration()@j) | (Ex #j. Healthy()@j)) "

verdictfunction v1:
    "Ex #i #k. Verdict()@i & Poisoned()@k" -> <'A'>,
    "Ex #i #k. Verdict()@i & Dehydration()@k" -> <'B'>,
    otherwise -> empty

lemma acc:
    v1 accounts [control]
    for "All #i . Verdict()@i ==> Ex #j. Healthy()@j"
    for parties <'A','B'>

end

  /* sanity (all-traces): verified (8 steps) */
  /* acc_excl_0_1 (all-traces): falsified - found trace (16 steps) */
  /* acc_excl_0_2 (all-traces): verified (2 steps) */
  /* acc_excl_1_2 (all-traces): verified (2 steps) */
  /* acc_exh (all-traces): verified (4 steps) */
  /* acc_suf_0 (exists-trace): verified (13 steps) */
  /* acc_suf_1 (exists-trace): verified (11 steps) */
  /* acc_compl_empty_2 (all-traces): falsified - found trace (6 steps) */
  /* acc_compl_nonempty_0 (all-traces): falsified - found trace (14 steps) */
  /* acc_compl_nonempty_1 (all-traces): verified (24 steps) */
  /* acc_min_0_0 (all-traces): verified (108 steps) */
  /* acc_min_1_0 (all-traces): verified (68 steps) */
  /* acc_uniq_0 (all-traces): verified (109 steps) */
  /* acc_uniq_1 (all-traces): verified (68 steps) */
  /* acc_rel_0_0 (all-traces): falsified - found trace (46 steps) */
  /* acc_rel_1_1 (all-traces): verified (632 steps) */
  /* acc_unrel_0_1 (all-traces): falsified - found trace (28 steps) */





