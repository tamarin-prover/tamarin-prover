theory NeedhamSchroeder

/*
 * Protocol:    Needham Schroeder Lowe for Secret Establising, without
 *              Server
 * Modeler:     Robert Kunnemann
 * Date:    February 2013
 *
 * Tamarin status : sanity1 and sanity2, < 1 sec
 *                  source lemma, < 2 sec on cluster
 *                  secrecy, 1 min on cluster
 * Proverif status : < 1 sec
 */

begin

builtins: asymmetric-encryption

process:
! ( new skA;
        event HonestA(pk(skA));
        out(pk(skA));
        !( in(pkB);
        new Na;
        event OUT_I_1(aenc(<Na,pk(skA)>,pkB));
        out(aenc( <Na,pk(skA) > ,pkB));
	in(cypher);
	let mess = adec(cypher,skA) in
	let <=Na,xNb,=pkB> = mess in
        event IN_I_2_nr(xNb,aenc(<Na,xNb,pkB>,pk(skA)));
        new k;
        event OUT_I_2(aenc(<xNb,k>,pkB));
        out(aenc(<xNb,k>,pkB));
        event SessionA(pk(skA),pkB,k)
        ) )
||
! ( new skB;
        event HonestB(pk(skB));
        out(pk(skB));
        !( in(cypher1);
	   let mess1 = adec(cypher1,skB) in
           let <xNa,pkA> = mess1 in
           event IN_R_1_ni(xNa,aenc(<xNa,pkA>,pk(skB)));
           new Nb;
           event OUT_R_1(aenc(<xNa,Nb,pk(skB)>,pkA));
           out(aenc(<xNa,Nb,pk(skB)>,pkA));
	   in(cypher2);
	   let mess2 = adec(cypher2,skB) in
	   let <=Nb,xk> = mess2 in
           event SessionB(pkA,pk(skB),xk)
        ))

 lemma sanity1: //make sure that a valid protocol run exists

      "not( Ex pka pkb k #t1 . SessionA(pka,pkb,k)@t1)"

 lemma sanity2:
      "not( Ex pka pkb k #t1 . SessionB(pka,pkb,k)@t1)"


/*
The source lemma bellow was true in the pattern matching based version, but is not in the new version.
Indeed, IN_R_1_ni may in fact accept messages either from the first or the second output of I, as they both have the same shape.

lemma source [sources, reuse]:
  " (All ni m1 #i.
       IN_R_1_ni( ni, m1) @ i
       ==>
       ( (Ex #j. KU(ni) @ j & j < i)
       | (Ex #j. OUT_I_1( m1 ) @ j)
       )
    )
  & (All nr m2 #i.
       IN_I_2_nr( nr, m2) @ i
       ==>
       ( (Ex #j. KU(nr) @ j & j < i)
       | (Ex #j. OUT_R_1( m2 ) @ j)
       )
    )
"
*/

lemma source [sources, reuse]:
  " (All ni m1 #i.
       IN_R_1_ni( ni, m1) @ i
       ==>
       ( (Ex #j. KU(ni) @ j & j < i)
       | (Ex #j. OUT_I_1( m1 ) @ j)
       | (Ex #j. OUT_I_2( m1 ) @ j)
       )
    )
  & (All nr m2 #i.
       IN_I_2_nr( nr, m2) @ i
       ==>
       ( (Ex #j. KU(nr) @ j & j < i)
       | (Ex #j. OUT_R_1( m2 ) @ j)
       )
    )
"
lemma secrecy:
        "not (
                Ex pka pkb k #t1 #t2 #i1 #i2.
                 SessionA(pka,pkb,k)@t1
                & KU(k)@t2
                &  HonestA(pka)@i1
                & HonestB(pkb)@i2
        )"

end
